HAI 2.0

OBTW
	Parallel NVE molecular dynamics simulation using Lennard-Jones potential
	and a velocity-verlet integrator implemented in LOLCODE /* DAR */
TLDR

OBTW
****
**** Define some functions
****
TLDR


BTW **** Compute kinetic energy

HOW IZ I COMPUTIN_MOVIN_ENERGY YR N AN YR VX AN YR VY AN YR VZ MKAY

 	I HAS A KE ITZ A NUMBAR AN ITZ 0 
	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		KE R SUM OF KE AN ...
			SUM OF SQUAR OF VX'Z I AN SUM OF SQUAR OF VY'Z I AN SQUAR OF VZ'Z I
	IM OUTTA YR LOOP
	KE R QUOSHUNT OF KE AN 2.0

	FOUND YR KE

IF U SAY SO


BTW **** Apply periodic boundary condition to particle

HOW IZ I KEEPIN_IT_IN_BOX YR D AN YR L MKAY

	I HAS A TMP AN ITZ A NUMBAR AN ITZ D

	I HAS A HL ITZ A NUMBAR AN ITZ QUOSHUNT OF L AN 2.0

	BOTH SAEM TMP AN BIGGR OF TMP AN HL, O RLY?
		YA RLY,
			TMP R DIFF OF TMP AN L
		MEBBE BOTH SAEM TMP AN SMALLR OF TMP AN DIFF OF 0 AN HL,
			TMP R SUM OF TMP AN L
	OIC

	FOUND YR TMP

IF U SAY SO


BTW **** Compute forces and potential energy

HOW IZ I FEELN_TEH_FORCE ...
	YR L ...
	AN YR RC2 AN YR ECUT AN YR ECOR ...
	AN YR N AN YR NN ...
	AN YR RX AN YR RY AN YR RZ ...
	AN YR FX AN YR FY AN YR FZ MKAY

	I HAS A J AN ITZ A NUMBR

	I HAS A DX ITZ A NUMBAR
	I HAS A DY ITZ A NUMBAR
	I HAS A DZ ITZ A NUMBAR

	I HAS A R2 ITZ A NUMBAR
	I HAS A R6 ITZ A NUMBAR

	I HAS A F ITZ A NUMBAR

	I HAS A PE ITZ A NUMBAR AN ITZ 0.0

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		FX'Z I R 0.0
		FY'Z I R 0.0
		FZ'Z I R 0.0
	IM OUTTA YR LOOP

	HUGZ

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N

		J R SUM OF I AN 1
		IM IN YR LOOP WILE DIFFRINT J AN N

			DX R DIFF OF RX'Z I AN RX'Z J	
			DY R DIFF OF RY'Z I AN RY'Z J	
			DZ R DIFF OF RZ'Z I AN RZ'Z J	

			R2 R SUM OF SQUAR OF DX AN SUM OF SQUAR OF DY AN SQUAR OF DZ

			DX R I IZ KEEPIN_IT_IN_BOX YR DX AN YR L MKAY
			DY R I IZ KEEPIN_IT_IN_BOX YR DY AN YR L MKAY
			DZ R I IZ KEEPIN_IT_IN_BOX YR DZ AN YR L MKAY

			BOTH SAEM R2 AN SMALLR OF R2 AN RC2, O RLY?
				YA RLY,
				R6 R FLIP OF POWR OF R2 AN 3
	
				PE R SUM OF PE ...
					AN DIFF OF PRODUKT OF 4 AN DIFF OF SQUAR OF R6 AN R6 AN ECUT

				F R PRODUKT OF 48.0 AN DIFF OF SQUAR OF R6 AN QUOSHUNT OF R6 AN 2

				FX'Z I R SUM OF FX'Z I AN PRODUKT OF DX AN QUOSHUNT OF F AN R2
				FX'Z J R DIFF OF FX'Z J AN PRODUKT OF DX AN QUOSHUNT OF F AN R2
				FY'Z I R SUM OF FY'Z I AN PRODUKT OF DY AN QUOSHUNT OF F AN R2
				FY'Z J R DIFF OF FY'Z J AN PRODUKT OF DY AN QUOSHUNT OF F AN R2
				FZ'Z I R SUM OF FZ'Z I AN PRODUKT OF DZ AN QUOSHUNT OF F AN R2
				FZ'Z J R DIFF OF FZ'Z J AN PRODUKT OF DZ AN QUOSHUNT OF F AN R2

			OIC

			J R SUM OF J AN 1
		IM OUTTA YR LOOP

	IM OUTTA YR LOOP

	IM IN YR LOOP UPPIN K TIL BOTH SAEM K AN 13

	TXT MAH BFF NN'Z K AN STUFF

	BTW IM SRSLY MESIN WIF UR FX
	BTW IM SRSLY MESIN WIF UR FY
	BTW IM SRSLY MESIN WIF UR FZ

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N

		IM IN YR LOOP UPPIN J TIL BOTH SAEM J AN N

			DX R DIFF OF RX'Z I AN UR RX'Z J	
			DY R DIFF OF RY'Z I AN UR RY'Z J	
			DZ R DIFF OF RZ'Z I AN UR RZ'Z J	

			R2 R SUM OF SQUAR OF DX AN SUM OF SQUAR OF DY AN SQUAR OF DZ

			DX R I IZ KEEPIN_IT_IN_BOX YR DX AN YR L MKAY
			DY R I IZ KEEPIN_IT_IN_BOX YR DY AN YR L MKAY
			DZ R I IZ KEEPIN_IT_IN_BOX YR DZ AN YR L MKAY

			BOTH SAEM R2 AN SMALLR OF R2 AN RC2, O RLY?
				YA RLY,
				R6 R FLIP OF POWR OF R2 AN 3
	
				PE R SUM OF PE ...
					AN DIFF OF PRODUKT OF 4 AN DIFF OF SQUAR OF R6 AN R6 AN ECUT

				F R PRODUKT OF 48.0 AN DIFF OF SQUAR OF R6 AN QUOSHUNT OF R6 AN 2

				FX'Z I R SUM OF FX'Z I AN PRODUKT OF DX AN QUOSHUNT OF F AN R2
				UR FX'Z J R DIFF OF UR FX'Z J AN PRODUKT OF DX AN QUOSHUNT OF F AN R2
				FY'Z I R SUM OF FY'Z I AN PRODUKT OF DY AN QUOSHUNT OF F AN R2
				UR FY'Z J R DIFF OF UR FY'Z J AN PRODUKT OF DY AN QUOSHUNT OF F AN R2
				FZ'Z I R SUM OF FZ'Z I AN PRODUKT OF DZ AN QUOSHUNT OF F AN R2
				UR FZ'Z J R DIFF OF UR FZ'Z J AN PRODUKT OF DZ AN QUOSHUNT OF F AN R2

			OIC

		IM OUTTA YR LOOP

	IM OUTTA YR LOOP

	BTW DUN MESIN WIF UR FX
	BTW DUN MESIN WIF UR FY
	BTW DUN MESIN WIF UR FZ

	TTYL

	IM OUTTA YR LOOP


	PE R SUM OF PE AN PRODUKT OF N AN ECOR

	FOUND YR PE

IF U SAY SO


I HAS A PIE ITZ A NUMBAR AN ITZ 3.14159

I HAS A N ITZ A NUMBR AN ITZ 216

WE HAS A RX ITZ LOTZ A NUMBARS AN THAR IZ N AN IM SHARIN IT
WE HAS A RY ITZ LOTZ A NUMBARS AN THAR IZ N AN IM SHARIN IT
WE HAS A RZ ITZ LOTZ A NUMBARS AN THAR IZ N AN IM SHARIN IT

I HAS A VX ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A VY ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A VZ ITZ LOTZ A NUMBARS AN THAR IZ N

WE HAS A FX ITZ LOTZ A NUMBARS AN THAR IZ N AN IM SHARIN IT
WE HAS A FY ITZ LOTZ A NUMBARS AN THAR IZ N AN IM SHARIN IT
WE HAS A FZ ITZ LOTZ A NUMBARS AN THAR IZ N AN IM SHARIN IT

I HAS A IX ITZ LOTZ A NUMBRS AN THAR IZ N
I HAS A IY ITZ LOTZ A NUMBRS AN THAR IZ N
I HAS A IZ ITZ LOTZ A NUMBRS AN THAR IZ N

I HAS A C ITZ A NUMBR
I HAS A A ITZ A NUMBR

I HAS A T ITZ A NUMBAR AN ITZ 0.0

WE HAS A CHILLN_ENERGY ITZ A NUMBAR AN IM SHARIN IT
WE HAS A MOVIN_ENERGY ITZ A NUMBAR AN IM SHARIN IT
WE HAS A ALL_OV_TEH_ENERGY ITZ A NUMBAR AN IM SHARIN IT

I HAS A T0 ITZ A NUMBAR AN ITZ 1.0

I HAS A I ITZ A NUMBR
I HAS A J ITZ A NUMBR
I HAS A S ITZ A NUMBR

I HAS A TMPX ITZ A NUMBAR
I HAS A TMPY ITZ A NUMBAR
I HAS A TMPZ ITZ A NUMBAR


OBTW
****
**** Initialize system
****
TLDR

BTW **** Define basic parameters for particle simulation

I HAS A NPES ITZ A NUMBR AN ITZ MAH FRENZ

I HAS A NPARTICLS ITZ A NUMBR AN ITZ PRODUKT OF NPES AN N

I HAS A RHO ITZ A NUMBAR AN ITZ 0.5

I HAS A V ITZ A NUMBAR AN ITZ QUOSHUNT OF NPARTICLS AN RHO

I HAS A L ITZ A NUMBAR AN ITZ ROOT OF V AN 3

I HAS A NSTEPS ITZ A NUMBR AN ITZ 100

I HAS A DT ITZ A NUMBAR AN ITZ 0.001

I HAS A DT2 ITZ A NUMBAR AN ITZ SQUAR OF DT


BTW **** Setup parallel decomposition 

I HAS A M ITZ A NUMBR AN ITZ 1

IM IN YR LOOP WILE DIFFRINT NPES AN SMALLR OF NPES AN POWR OF M AN 3
	M R SUM OF M AN 1
IM OUTTA YR LOOP

DIFFRINT NPES AN POWR OF M AN 3, O RLY?
	YA RLY,
		RTFM "BAD NPES"
OIC

I HAS A CELLX ITZ A NUMBR AN ITZ MOD OF ME AN M
I HAS A CELLY ITZ A NUMBR AN ITZ MOD OF QUOSHUNT OF ME AN M AN M
I HAS A CELLZ ITZ A NUMBR AN ITZ QUOSHUNT OF QUOSHUNT OF ME AN M AN M


BTW **** WONT U B MAH NAYBR

I HAS A NN ITZ LOTZ A NUMBRS AN THAR IZ 13

IM IN YR LOOP UPPIN J FRUM 14 TIL BOTH SAEM J AN 27
	I HAS A JX ITZ A NUMBR
	I HAS A JY ITZ A NUMBR
	I HAS A JZ ITZ A NUMBR
	I HAS A JPE ITZ A NUMBR
	JX R MOD OF SUM OF CELLX AN SUM OF M AN MOD OF J AN 3 AN M
	JY R MOD OF SUM OF CELLY AN SUM OF M AN MOD OF QUOSHUNT OF J AN 3 AN 3 AN M
	JZ R MOD OF SUM OF CELLZ AN SUM OF M AN QUOSHUNT OF J AN SQUAR OF 3 AN M
	JPE R SUM OF PRODUKT OF JZ AN SQUAR OF M AN SUM OF PRODUKT OF JY AN M AN JX
	I R DIFF OF J AN 14
	NN'Z I R JPE
IM OUTTA YR LOOP

I HAS A N3 ITZ A NUMBR AN ITZ 2

IM IN YR LOOP WILE DIFFRINT N AN SMALLR OF N AN POWR OF N3 AN 3
	N3 R SUM OF N3 AN 1
IM OUTTA YR LOOP


BTW *** Compute particle potential

I HAS A RC2 ITZ A NUMBAR AN ITZ 25.0

I HAS A RR3 ITZ A NUMBAR AN ITZ FLIP OF POWR OF RC2 AN 3

I HAS A RR9 ITZ A NUMBAR AN ITZ PRODUKT OF PRODUKT OF RR3 AN RR3 AN RR3

I HAS A ECOR ITZ A NUMBAR AN ITZ ...
	PRODUKT OF 8 AN PRODUKT OF PIE AN PRODUKT OF RHO ...
		AN	DIFF OF QUOSHUNT OF RR9 AN 9 AN QUOSHUNT OF RR3 AN 3

I HAS A PCOR ITZ A NUMBAR AN ITZ ...
	PRODUKT OF QUOSHUNT OF 16.0 AN 3.0 AN PRODUKT OF PIE ...
		AN PRODUKT OF SQUAR OF RHO AN DIFF OF ...
				PRODUKT OF QUOSHUNT OF 2.0 AN 3.0 AN RR9 AN RR3

I HAS A ECUT ITZ A NUMBAR AN ITZ ...
	PRODUKT OF 4 AN PRODUKT OF SQUAR OF RR3 AN DIFF OF SQUAR OF RR3 AN 1


BTW **** Initialize particle positions ****

I HAS A IIX0 ITZ A NUMBR AN ITZ PRODUKT OF CELLX AN N3
I HAS A IIY0 ITZ A NUMBR AN ITZ PRODUKT OF CELLY AN N3
I HAS A IIZ0 ITZ A NUMBR AN ITZ PRODUKT OF CELLZ AN N3

I HAS A IIX ITZ A NUMBR AN ITZ 0
I HAS A IIY ITZ A NUMBR AN ITZ 0
I HAS A IIZ ITZ A NUMBR AN ITZ 0

I HAS A A0 ITZ A NUMBAR AN ITZ QUOSHUNT OF L AN PRODUKT OF N3 AN M

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	RX'Z I R PRODUKT OF SUM OF IIX0 AN SUM OF IIX AN 0.5 AN A0
	RY'Z I R PRODUKT OF SUM OF IIY0 AN SUM OF IIY AN 0.5 AN A0
	RZ'Z I R PRODUKT OF SUM OF IIZ0 AN SUM OF IIZ AN 0.5 AN A0
	IIX R SUM OF IIX AN 1
	BOTH SAEM IIX AN N3, O RLY?
		YA RLY,
			IIX R 0
			IIY R SUM OF IIY AN 1
			BOTH SAEM IIY AN N3, O RLY?
				YA RLY,
					IIY R 0
					IIZ R SUM OF IIZ AN 1
			OIC
	OIC
	TMPX R RX'Z I
	TMPY R RY'Z I
	TMPZ R RZ'Z I
IM OUTTA YR LOOP


BTW **** Initialize particle velocities ****

I HAS A CMX ITZ A NUMBAR AN ITZ 0.0
I HAS A CMY ITZ A NUMBAR AN ITZ 0.0
I HAS A CMZ ITZ A NUMBAR AN ITZ 0.0

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	VX'Z I R WHATEVAR
	VY'Z I R WHATEVAR
	VZ'Z I R WHATEVAR
	CMX R SUM OF CMX AN VX'Z I
	CMY R SUM OF CMY AN VY'Z I
	CMZ R SUM OF CMZ AN VZ'Z I
IM OUTTA YR LOOP

CMX R QUOSHUNT OF CMX AN N
CMY R QUOSHUNT OF CMY AN N
CMZ R QUOSHUNT OF CMZ AN N

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	VX'Z I R DIFF OF VX'Z I AN CMX
	VY'Z I R DIFF OF VY'Z I AN CMY
	VZ'Z I R DIFF OF VZ'Z I AN CMZ
IM OUTTA YR LOOP

MOVIN_ENERGY R I IZ COMPUTIN_MOVIN_ENERGY YR N AN YR VX AN YR VY AN YR VZ MKAY

DIFFRINT T0 AN 0.0, O RLY?
	YA RLY,
		T R PRODUKT OF QUOSHUNT OF MOVIN_ENERGY AN N AN QUOSHUNT OF 2.0 AN 3.0
		I HAS A FAC ITZ A NUMBAR AN ITZ UNSQUAR OF QUOSHUNT OF T0 AN T
		IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
			VX'Z I R PRODUKT OF VX'Z I AN FAC
			VY'Z I R PRODUKT OF VY'Z I AN FAC
			VZ'Z I R PRODUKT OF VZ'Z I AN FAC
		IM OUTTA YR LOOP
OIC

MOVIN_ENERGY R I IZ COMPUTIN_MOVIN_ENERGY YR N AN YR VX AN YR VY AN YR VZ MKAY


BTW *** Initialize boundary crossing counter

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	IX'Z I R 0
	IY'Z I R 0
	IZ'Z I R 0
IM OUTTA YR LOOP

OBTW
****
**** Start simulation
****
TLDR

HUGZ

CHILLN_ENERGY R I IZ FEELN_TEH_FORCE ...
	YR L AN YR RC2 AN YR ECUT AN YR ECOR AN YR N AN YR NN ...
	AN YR RX AN YR RY AN YR RZ AN YR FX AN YR FY AN YR FZ MKAY

HUGZ

IM IN YR LOOP UPPIN S TIL BOTH SAEM S AN NSTEPS

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N

		RX'Z I R SUM OF RX'Z I AN SUM OF PRODUKT OF DT AN VX'Z I ...
			AN PRODUKT OF PRODUKT OF 0.5 AN DT2 AN FX'Z I

		RY'Z I R SUM OF RY'Z I AN SUM OF PRODUKT OF DT AN VY'Z I ...
			AN PRODUKT OF PRODUKT OF 0.5 AN DT2 AN FY'Z I

		RZ'Z I R SUM OF RZ'Z I AN SUM OF PRODUKT OF DT AN VZ'Z I ...
			AN PRODUKT OF PRODUKT OF 0.5 AN DT2 AN FZ'Z I

		VX'Z I R SUM OF VX'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FX'Z I

		VY'Z I R SUM OF VY'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FY'Z I

		VZ'Z I R SUM OF VZ'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FZ'Z I
		
		BOTH SAEM RX'Z I AN SMALLR OF RX'Z I AN 0, O RLY?
			YA RLY,
				RX'Z I R SUM OF RX'Z I AN L
				IX'Z I R DIFF OF IX'Z I AN L
		OIC

		BOTH SAEM RX'Z I AN BIGGR OF RX'Z I AN L, O RLY?
			YA RLY,
				RX'Z I R DIFF OF RX'Z I AN L
				IX'Z I R SUM OF IX'Z I AN L
		OIC

		BOTH SAEM RY'Z I AN SMALLR OF RY'Z I AN 0, O RLY?
			YA RLY,
				RY'Z I R SUM OF RY'Z I AN L
				IY'Z I R DIFF OF IY'Z I AN L
		OIC

		BOTH SAEM RY'Z I AN BIGGR OF RY'Z I AN L, O RLY?
			YA RLY,
				RY'Z I R DIFF OF RY'Z I AN L
				IY'Z I R SUM OF IY'Z I AN L
		OIC

		BOTH SAEM RZ'Z I AN SMALLR OF RZ'Z I AN 0, O RLY?
			YA RLY,
				RZ'Z I R SUM OF RZ'Z I AN L
				IZ'Z I R DIFF OF IZ'Z I AN L
		OIC

		BOTH SAEM RZ'Z I AN BIGGR OF RZ'Z I AN L, O RLY?
			YA RLY,
				RZ'Z I R DIFF OF RZ'Z I AN L
				IZ'Z I R SUM OF IZ'Z I AN L
		OIC

	IM OUTTA YR LOOP

HUGZ

	CHILLN_ENERGY R I IZ FEELN_TEH_FORCE ...
		YR L AN YR RC2 AN YR ECUT AN YR ECOR AN YR N AN YR NN ...
		AN YR RX AN YR RY AN YR RZ AN YR FX AN YR FY AN YR FZ MKAY

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		VX'Z I R SUM OF VX'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FX'Z I
		VY'Z I R SUM OF VY'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FY'Z I
		VZ'Z I R SUM OF VZ'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FZ'Z I
	IM OUTTA YR LOOP

	MOVIN_ENERGY R I IZ COMPUTIN_MOVIN_ENERGY ...
		YR N AN YR VX AN YR VY AN YR VZ MKAY

	HUGZ
	BOTH SAEM ME AN 0, O RLY?
		YA RLY,
			IM IN YR LOOP UPPIN K FRUM 1 TIL BOTH SAEM K AN MAH FRENZ
				TXT MAH BFF K, 
					MOVIN_ENERGY R SUM OF MOVIN_ENERGY AN UR MOVIN_ENERGY
				TXT MAH BFF K,
					CHILLN_ENERGY R SUM OF CHILLN_ENERGY AN UR CHILLN_ENERGY
			IM OUTTA YR LOOP
	OIC
	HUGZ
	DIFFRINT ME AN 0, O RLY?
		YA RLY,
			TXT MAH BFF 0, MOVIN_ENERGY R UR MOVIN_ENERGY
			TXT MAH BFF 0, CHILLN_ENERGY R UR CHILLN_ENERGY
	OIC

	ALL_OV_TEH_ENERGY R SUM OF CHILLN_ENERGY AN MOVIN_ENERGY

	BTW **** Output atom positions in xyz format

	BOTH SAEM ME AN 0, O RLY?
		YA RLY,
			VISIBLE NPARTICLS
			VISIBLE "# PE KE TE" CHILLN_ENERGY MOVIN_ENERGY ALL_OV_TEH_ENERGY 
	OIC

	IM IN YR LOOP UPPIN K TIL BOTH SAEM K AN MAH FRENZ
		BOTH SAEM ME AN K, O RLY?
			YA RLY,
				IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
					TMPX R RX'Z I
					TMPY R RY'Z I
					TMPZ R RZ'Z I
					VISIBLE 16 TMPX TMPY TMPZ
				IM OUTTA YR LOOP
		OIC
		HUGZ
	IM OUTTA YR LOOP

IM OUTTA YR LOOP

BTW **** The next line is a complete hack -DAR
I IZ KEEPIN_IT_IN_BOX YR 0.0 AN YR 1.0 MKAY

KTHXBYE


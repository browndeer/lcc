HAI 2.0

OBTW
	Parallel molecular dynamics simulation using Lennard-Jones potential
	and a velocity-verlet integrator implemented in LOLCODE /* DAR */
TLDR

I HAS A PIE ITZ A NUMBAR AN ITZ 3.14159

I HAS A N ITZ A NUMBR AN ITZ 216

I HAS A RX ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A RY ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A RZ ITZ LOTZ A NUMBARS AN THAR IZ N

I HAS A VX ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A VY ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A VZ ITZ LOTZ A NUMBARS AN THAR IZ N

I HAS A FX ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A FY ITZ LOTZ A NUMBARS AN THAR IZ N
I HAS A FZ ITZ LOTZ A NUMBARS AN THAR IZ N

I HAS A IX ITZ LOTZ A NUMBRS AN THAR IZ N
I HAS A IY ITZ LOTZ A NUMBRS AN THAR IZ N
I HAS A IZ ITZ LOTZ A NUMBRS AN THAR IZ N

I HAS A C ITZ A NUMBR
I HAS A A ITZ A NUMBR

I HAS A L ITZ A NUMBAR AN ITZ 0.0
I HAS A HL ITZ A NUMBAR
I HAS A RHO ITZ A NUMBAR AN ITZ 0.5
I HAS A T ITZ A NUMBAR AN ITZ 0.0
I HAS A RC2 ITZ A NUMBAR AN ITZ 5.0

I HAS A PCOR ITZ A NUMBAR
I HAS A V ITZ A NUMBAR

I HAS A CHILLN_ENERGY ITZ A NUMBAR
I HAS A MOVIN_ENERGY ITZ A NUMBAR
I HAS A ALL_OV_TEH_ENERGY ITZ A NUMBAR

I HAS A ECOR ITZ A NUMBAR
I HAS A ECUT ITZ A NUMBAR
I HAS A T0 ITZ A NUMBAR AN ITZ 1.0

I HAS A RR3 ITZ A NUMBAR
I HAS A RR9 ITZ A NUMBAR
I HAS A DT ITZ A NUMBAR AN ITZ 0.001
I HAS A DT2 ITZ A NUMBAR

I HAS A I ITZ A NUMBR
I HAS A J ITZ A NUMBR
I HAS A S ITZ A NUMBR

I HAS A NSTEPS ITZ A NUMBR AN ITZ 10

I HAS A DX ITZ A NUMBAR
I HAS A DY ITZ A NUMBAR
I HAS A DZ ITZ A NUMBAR

I HAS A R2 ITZ A NUMBAR
I HAS A R6 ITZ A NUMBAR
I HAS A F ITZ A NUMBAR
I HAS A FAC ITZ A NUMBAR
I HAS A E ITZ A NUMBAR

V R QUOSHUNT OF N AN RHO
L R ROOT OF V AN 3
HL R QUOSHUNT OF L AN 2.0

RR3 R FLIP OF POWR OF RC2 AN 3

RR9 R PRODUKT OF PRODUKT OF RR3 AN RR3 AN RR3

ECOR R PRODUKT OF 8 AN PRODUKT OF PIE AN PRODUKT OF RHO ...
	AN	DIFF OF QUOSHUNT OF RR9 AN 9 AN QUOSHUNT OF RR3 AN 3

PCOR R PRODUKT OF QUOSHUNT OF 16.0 AN 3.0 AN PRODUKT OF PIE ...
			AN PRODUKT OF SQUAR OF RHO AN DIFF OF ...
					PRODUKT OF QUOSHUNT OF 2.0 AN 3.0 AN RR9 AN RR3

ECUT R PRODUKT OF 4 AN PRODUKT OF SQUAR OF RR3 AN DIFF OF SQUAR OF RR3 AN 1

BTW VISIBLE " ECUT " ECUT

RC2 R SQUAR OF RC2

DT2 R SQUAR OF DT

I HAS A TMPX ITZ A NUMBAR
I HAS A TMPY ITZ A NUMBAR
I HAS A TMPZ ITZ A NUMBAR

OBTW
****
**** Define lambda functions
****
TLDR

BTW **** Compute kinetic energy

HOW IZ I COMPUTIN_MOVIN_ENERGY

 	I HAS A VAR ITZ A NUMBAR AN ITZ 0
	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		VAR R SUM OF VAR AN ...
			SUM OF SQUAR OF VX'Z I AN SUM OF SQUAR OF VY'Z I AN SQUAR OF VZ'Z I
	IM OUTTA YR LOOP
	VAR R QUOSHUNT OF VAR AN 2.0
	MOVIN_ENERGY R VAR
	FOUND YR VAR

IF U SAY SO

BTW **** Correct for boundary crossings

HOW IZ I KEEPIN_PARTICLS_IN_BOX
	BOTH SAEM DX AN BIGGR OF DX AN HL, O RLY?
		YA RLY,
			DX R DIFF OF DX AN L
		MEBBE BOTH SAEM DX AN SMALLR OF DX AN DIFF OF 0 AN HL,
			DX R SUM OF DX AN L
	OIC
	BOTH SAEM DY AN BIGGR OF DY AN HL, O RLY?
		YA RLY,
			DY R DIFF OF DY AN L
		MEBBE BOTH SAEM DY AN SMALLR OF DY AN DIFF OF 0 AN HL,
			DY R SUM OF DY AN L
	OIC
	BOTH SAEM DZ AN BIGGR OF DZ AN HL, O RLY?
		YA RLY,
			DZ R DIFF OF DZ AN L
		MEBBE BOTH SAEM DZ AN SMALLR OF DZ AN DIFF OF 0 AN HL,
			DZ R SUM OF DZ AN L
	OIC
	FOUND YR 0
IF U SAY SO

BTW **** Compute forces and potential energy

HOW IZ I FEELN_TEH_FORCE

	E R 0.0

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		FX'Z I R 0.0
		FY'Z I R 0.0
		FZ'Z I R 0.0
	IM OUTTA YR LOOP

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N

		J R SUM OF I AN 1
		IM IN YR LOOP WILE DIFFRINT J AN N

			DX R DIFF OF RX'Z I AN RX'Z J	
			DY R DIFF OF RY'Z I AN RY'Z J	
			DZ R DIFF OF RZ'Z I AN RZ'Z J	
			R2 R SUM OF SQUAR OF DX AN SUM OF SQUAR OF DY AN SQUAR OF DZ

			I IZ KEEPIN_PARTICLS_IN_BOX

			BOTH SAEM R2 AN SMALLR OF R2 AN RC2, O RLY?
				YA RLY,
				R6 R FLIP OF POWR OF R2 AN 3
	
				E R SUM OF E ...
					AN DIFF OF PRODUKT OF 4 AN DIFF OF SQUAR OF R6 AN R6 AN ECUT

				F R PRODUKT OF 48.0 AN DIFF OF SQUAR OF R6 AN QUOSHUNT OF R6 AN 2

				FX'Z I R SUM OF FX'Z I AN PRODUKT OF DX AN QUOSHUNT OF F AN R2
				FX'Z J R DIFF OF FX'Z J AN PRODUKT OF DX AN QUOSHUNT OF F AN R2
				FY'Z I R SUM OF FY'Z I AN PRODUKT OF DY AN QUOSHUNT OF F AN R2
				FY'Z J R DIFF OF FY'Z J AN PRODUKT OF DY AN QUOSHUNT OF F AN R2
				FZ'Z I R SUM OF FZ'Z I AN PRODUKT OF DZ AN QUOSHUNT OF F AN R2
				FZ'Z J R DIFF OF FZ'Z J AN PRODUKT OF DZ AN QUOSHUNT OF F AN R2
			OIC

			J R SUM OF J AN 1
		IM OUTTA YR LOOP
	IM OUTTA YR LOOP

	CHILLN_ENERGY R SUM OF E AN PRODUKT OF N AN ECOR

	FOUND YR CHILLN_ENERGY

IF U SAY SO


OBTW
****
**** Initialize system
****
TLDR

I HAS A N3 ITZ A NUMBR AN ITZ 2

IM IN YR LOOP WILE DIFFRINT N AN SMALLR OF N AN POWR OF N3 AN 3
	N3 R SUM OF N3 AN 1
IM OUTTA YR LOOP

BTW **** Initialize particle positions ****

I HAS A IIX ITZ A NUMBR AN ITZ 0
I HAS A IIY ITZ A NUMBR AN ITZ 0
I HAS A IIZ ITZ A NUMBR AN ITZ 0

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	RX'Z I R PRODUKT OF SUM OF IIX AN 0.5 AN QUOSHUNT OF L AN N3
	RY'Z I R PRODUKT OF SUM OF IIY AN 0.5 AN QUOSHUNT OF L AN N3
	RZ'Z I R PRODUKT OF SUM OF IIZ AN 0.5 AN QUOSHUNT OF L AN N3
	IIX R SUM OF IIX AN 1
	BOTH SAEM IIX AN N3, O RLY?
		YA RLY,
			IIX R 0
			IIY R SUM OF IIY AN 1
			BOTH SAEM IIY AN N3, O RLY?
				YA RLY,
					IIY R 0
					IIZ R SUM OF IIZ AN 1
			OIC
	OIC
IM OUTTA YR LOOP

BTW **** Initialize particle velocities ****

I HAS A CMX ITZ A NUMBAR AN ITZ 0.0
I HAS A CMY ITZ A NUMBAR AN ITZ 0.0
I HAS A CMZ ITZ A NUMBAR AN ITZ 0.0

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	VX'Z I R WHATEVAR
	VY'Z I R WHATEVAR
	VZ'Z I R WHATEVAR
	CMX R SUM OF CMX AN VX'Z I
	CMY R SUM OF CMY AN VY'Z I
	CMZ R SUM OF CMZ AN VZ'Z I
IM OUTTA YR LOOP

CMX R QUOSHUNT OF CMX AN N
CMY R QUOSHUNT OF CMY AN N
CMZ R QUOSHUNT OF CMZ AN N

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	VX'Z I R DIFF OF VX'Z I AN CMX
	VY'Z I R DIFF OF VY'Z I AN CMY
	VZ'Z I R DIFF OF VZ'Z I AN CMZ
IM OUTTA YR LOOP

I IZ COMPUTIN_MOVIN_ENERGY

DIFFRINT T0 AN 0.0, O RLY?
	YA RLY,
		T R PRODUKT OF QUOSHUNT OF MOVIN_ENERGY AN N AN QUOSHUNT OF 2.0 AN 3.0
		FAC R UNSQUAR OF QUOSHUNT OF T0 AN T
		IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
			VX'Z I R PRODUKT OF VX'Z I AN FAC
			VY'Z I R PRODUKT OF VY'Z I AN FAC
			VZ'Z I R PRODUKT OF VZ'Z I AN FAC
		IM OUTTA YR LOOP
OIC

I IZ COMPUTIN_MOVIN_ENERGY

BTW *** Initialize boundary crossing counter

IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
	IX'Z I R 0
	IY'Z I R 0
	IZ'Z I R 0
IM OUTTA YR LOOP

OBTW
****
**** Start simulation
****
TLDR

I IZ FEELN_TEH_FORCE

IM IN YR LOOP UPPIN S TIL BOTH SAEM S AN NSTEPS

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N

		RX'Z I R SUM OF RX'Z I AN SUM OF PRODUKT OF DT AN VX'Z I ...
			AN PRODUKT OF PRODUKT OF 0.5 AN DT2 AN FX'Z I

		RY'Z I R SUM OF RY'Z I AN SUM OF PRODUKT OF DT AN VY'Z I ...
			AN PRODUKT OF PRODUKT OF 0.5 AN DT2 AN FY'Z I

		RZ'Z I R SUM OF RZ'Z I AN SUM OF PRODUKT OF DT AN VZ'Z I ...
			AN PRODUKT OF PRODUKT OF 0.5 AN DT2 AN FZ'Z I

		VX'Z I R SUM OF VX'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FX'Z I

		VY'Z I R SUM OF VY'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FY'Z I

		VZ'Z I R SUM OF VZ'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FZ'Z I
		
		BOTH SAEM RX'Z I AN SMALLR OF RX'Z I AN 0, O RLY?
			YA RLY,
				RX'Z I R SUM OF RX'Z I AN L
				IX'Z I R DIFF OF IX'Z I AN L
		OIC

		BOTH SAEM RX'Z I AN BIGGR OF RX'Z I AN L, O RLY?
			YA RLY,
				RX'Z I R DIFF OF RX'Z I AN L
				IX'Z I R SUM OF IX'Z I AN L
		OIC

		BOTH SAEM RY'Z I AN SMALLR OF RY'Z I AN 0, O RLY?
			YA RLY,
				RY'Z I R SUM OF RY'Z I AN L
				IY'Z I R DIFF OF IY'Z I AN L
		OIC

		BOTH SAEM RY'Z I AN BIGGR OF RY'Z I AN L, O RLY?
			YA RLY,
				RY'Z I R DIFF OF RY'Z I AN L
				IY'Z I R SUM OF IY'Z I AN L
		OIC

		BOTH SAEM RZ'Z I AN SMALLR OF RZ'Z I AN 0, O RLY?
			YA RLY,
				RZ'Z I R SUM OF RZ'Z I AN L
				IZ'Z I R DIFF OF IZ'Z I AN L
		OIC

		BOTH SAEM RZ'Z I AN BIGGR OF RZ'Z I AN L, O RLY?
			YA RLY,
				RZ'Z I R DIFF OF RZ'Z I AN L
				IZ'Z I R SUM OF IZ'Z I AN L
		OIC

	IM OUTTA YR LOOP

	I IZ FEELN_TEH_FORCE

	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		VX'Z I R SUM OF VX'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FX'Z I
		VY'Z I R SUM OF VY'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FY'Z I
		VZ'Z I R SUM OF VZ'Z I AN PRODUKT OF 0.5 AN PRODUKT OF DT AN FZ'Z I
	IM OUTTA YR LOOP

	I IZ COMPUTIN_MOVIN_ENERGY

	ALL_OV_TEH_ENERGY R SUM OF CHILLN_ENERGY AN MOVIN_ENERGY

	BTW **** Output atom positions in xyz format

	VISIBLE N
	VISIBLE "# PE KE TE" CHILLN_ENERGY MOVIN_ENERGY ALL_OV_TEH_ENERGY 
	IM IN YR LOOP UPPIN I TIL BOTH SAEM I AN N
		TMPX R RX'Z I
		TMPY R RY'Z I
		TMPZ R RZ'Z I
		VISIBLE 16 TMPX TMPY TMPZ
	IM OUTTA YR LOOP

IM OUTTA YR LOOP

KTHXBYE

